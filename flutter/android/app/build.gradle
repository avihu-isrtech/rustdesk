import com.google.protobuf.gradle.*

plugins {
    id "com.google.protobuf" version "0.9.4"
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

dependencies {
    implementation 'com.google.protobuf:protobuf-javalite:3.20.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.20.1'
    }

    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

android {
    namespace "com.isrtech.isr.remote"
    compileSdkVersion 34
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        main.proto.srcDirs += '../../../libs/hbb_common/protos'
        main.proto.includes += "message.proto"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId "com.isrtech.isr.remote"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias System.getenv("RUSTDESK_ANDROID_KEY_ALIAS")
            keyPassword System.getenv("RUSTDESK_ANDROID_KEY_PASSWORD")
            storeFile file(System.getenv("RUSTDESK_ANDROID_KEY_PATH"))
            storePassword System.getenv("RUSTDESK_ANDROID_STORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules'
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'
    implementation "androidx.media:media:1.6.0"
    implementation 'com.github.getActivity:XXPermissions:18.5'
    implementation("org.jetbrains.kotlin:kotlin-stdlib") { version { strictly("1.9.10") } }
    implementation 'com.caverock:androidsvg-aar:1.4'
}
